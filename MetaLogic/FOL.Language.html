<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc-markdown-css-theme" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <title>FOL.Language</title>
  <link rel="stylesheet" href="css/theme.css" />
  <link rel="stylesheet" href="css/skylighting-solarized-theme.css" />
  <script type="text/javascript" src="highlight-hover.js"></script>
  <script defer="" src="https://cdn.jsdelivr.net/npm/katex@0.15.1/dist/katex.min.js"></script>
  <script>document.addEventListener("DOMContentLoaded", function () {
 var mathElements = document.getElementsByClassName("math");
 var macros = [];
 for (var i = 0; i < mathElements.length; i++) {
  var texText = mathElements[i].firstChild;
  if (mathElements[i].tagName == "SPAN") {
   katex.render(texText.data, mathElements[i], {
    displayMode: mathElements[i].classList.contains('display'),
    throwOnError: false,
    macros: macros,
    fleqn: false
   });
}}});
  </script>
  <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/katex@0.15.1/dist/katex.min.css" />
</head>
<body>

<header>
<h1 class="title"></h1>
<blockquote class="metadata">
</blockquote>
</header>

<nav id="TOC" role="doc-toc">
    <strong>Contents</strong><label for="contents">⊕</label>
  <input type="checkbox" id="contents">
  <ul>
  <li><a href="#一阶逻辑-语言" id="toc-一阶逻辑-语言">一阶逻辑 ▸ 语言</a></li>
  </ul>
</nav>

<main>
<h1 id="一阶逻辑-语言">一阶逻辑 ▸ 语言</h1>
<p>一阶逻辑是一种形式语言, 其语句由一些原始符号按一定的语法组合而成. 符号又分为逻辑符号和非逻辑符号. 本篇先讲非逻辑符号.</p>
<p>非逻辑符号分为函数符号和关系符号, 且它们都带有一个称为元数 (arity) 的属性. 例如, 元数为 2 的函数符号即用于表示二元函数. 特别地, 元数为零的函数又称为常量.</p>
<p>较传统的处理方式是给出所有可能的函数符号和关系符号. 即对任意元数 <span class="math inline">n</span>, 都有自然数多个函数符号</p>
<p><span class="math display">f^n_0,\ f^n_1,\ f^n_2,\ f^n_3,\ ...</span></p>
<p>以及自然数多个关系符号</p>
<p><span class="math display">R^n_0,\ R^n_1,\ R^n_2,\ R^n_3,\ ...</span></p>
<p>在这种处理下, 只有唯一一种一阶逻辑语言.</p>
<p>较现代的方式是根据最终要实现的一阶逻辑语言来指定该理论所需的非逻辑符号. 这些特定的符号以及它们的元数所组成的资料叫做理论的<strong>签名 (signature)</strong>. 在这种处理下, 每种签名都对应一种一阶逻辑语言, 因此签名又叫做<strong>语言 (language)</strong>, 语言的实例按惯例记作 ℒ. 由于一阶逻辑的其他部分都是参数化到语言的, 我们把它单独作为一个模块.</p>
<pre class="Agda"><a id="557" class="Keyword">open</a> <a id="562" class="Keyword">import</a> <a id="569" href="Foundation.Essential.html" class="Module">Foundation.Essential</a>

<a id="591" class="Keyword">module</a> <a id="598" href="FOL.Language.html" class="Module">FOL.Language</a> <a id="611" class="Keyword">where</a>
</pre>
<p><strong><u>定义</u></strong> 一阶逻辑的语言 <code>ℒ</code> 是一个六元组</p>
<ol type="1">
<li>离散的函数符号集 <code>𝓕</code></li>
<li>离散的关系符号集 <code>𝓡</code></li>
<li><code>𝓕</code> 到元数的映射 <code>∣_∣ᶠ</code></li>
<li><code>𝓡</code> 到元数的映射 <code>∣_∣ᴿ</code></li>
<li><code>𝓕</code> 的一个枚举</li>
<li><code>𝓡</code> 的一个枚举</li>
</ol>
<pre class="Agda"><a id="765" class="Keyword">record</a> <a id="Language"></a><a id="772" href="FOL.Language.html#772" class="Record">Language</a> <a id="781" class="Symbol">:</a> <a id="783" href="Agda.Primitive.html#388" class="Primitive">𝕋₁</a> <a id="786" class="Keyword">where</a>
  <a id="794" class="Keyword">constructor</a> <a id="mkLang"></a><a id="806" href="FOL.Language.html#806" class="InductiveConstructor">mkLang</a>
  <a id="815" class="Keyword">field</a>
    <a id="Language.𝓕"></a><a id="825" href="FOL.Language.html#825" class="Field">𝓕</a> <a id="827" class="Symbol">:</a> <a id="829" href="Agda.Primitive.html#388" class="Primitive">𝕋</a>
    <a id="Language.𝓡"></a><a id="835" href="FOL.Language.html#835" class="Field">𝓡</a> <a id="837" class="Symbol">:</a> <a id="839" href="Agda.Primitive.html#388" class="Primitive">𝕋</a>
    <a id="Language.∣_∣ᶠ"></a><a id="845" href="FOL.Language.html#845" class="Field Operator">∣_∣ᶠ</a> <a id="850" class="Symbol">:</a> <a id="852" href="FOL.Language.html#825" class="Field">𝓕</a> <a id="854" class="Symbol">→</a> <a id="856" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
    <a id="Language.∣_∣ᴿ"></a><a id="862" href="FOL.Language.html#862" class="Field Operator">∣_∣ᴿ</a> <a id="867" class="Symbol">:</a> <a id="869" href="FOL.Language.html#835" class="Field">𝓡</a> <a id="871" class="Symbol">→</a> <a id="873" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
    <a id="879" class="Symbol">⦃</a> <a id="Language.discr𝓕"></a><a id="881" href="FOL.Language.html#881" class="Field">discr𝓕</a> <a id="888" class="Symbol">⦄</a> <a id="890" class="Symbol">:</a> <a id="892" href="Foundation.Relation.Nullary.Discrete.Base.html#436" class="Function">discrete</a> <a id="901" href="FOL.Language.html#825" class="Field">𝓕</a>
    <a id="907" class="Symbol">⦃</a> <a id="Language.discr𝓡"></a><a id="909" href="FOL.Language.html#909" class="Field">discr𝓡</a> <a id="916" class="Symbol">⦄</a> <a id="918" class="Symbol">:</a> <a id="920" href="Foundation.Relation.Nullary.Discrete.Base.html#436" class="Function">discrete</a> <a id="929" href="FOL.Language.html#835" class="Field">𝓡</a>
    <a id="935" class="Symbol">⦃</a> <a id="Language.enum𝓕"></a><a id="937" href="FOL.Language.html#937" class="Field">enum𝓕</a> <a id="943" class="Symbol">⦄</a> <a id="945" class="Symbol">:</a> <a id="947" href="Foundation.Function.Enumeration.ListView.Base.html#2155" class="Record">Enum</a> <a id="952" href="FOL.Language.html#825" class="Field">𝓕</a>
    <a id="958" class="Symbol">⦃</a> <a id="Language.enum𝓡"></a><a id="960" href="FOL.Language.html#960" class="Field">enum𝓡</a> <a id="966" class="Symbol">⦄</a> <a id="968" class="Symbol">:</a> <a id="970" href="Foundation.Function.Enumeration.ListView.Base.html#2155" class="Record">Enum</a> <a id="975" href="FOL.Language.html#835" class="Field">𝓡</a>
</pre>
<p><strong><u>注意</u></strong> 在经典语境下集合一定是离散的, 但在直觉主义 HoTT 中, 离散强于“集合”. 因此当我们要求某 <code>A</code> 是“离散集”的时候, 实际上只要求它是离散类型, 然后它自然是一个集合.</p>
<p><strong><u>注意</u></strong> 回顾<a href="https://www.yuque.com/ocau/metalogic/foundation.essential#c1933822">枚举的定义</a>, 某类型 <code>A</code> 的枚举 <code>Enum A</code>是一个二元组: 1. 满足 <code>wit</code> 的一个函数 <code>enum</code> 2. <code>enum</code> 的一个 <code>cum</code></p>
<p><strong><u>约定</u></strong> 对于一个类型, 我们自始至终只会谈论它的一个枚举. 所以对任意类型的枚举, 我们都会用 <code>enum</code>, <code>wit</code> 和 <code>cum</code> 来指代该类型的那个我们唯一谈论的枚举的 <code>enum</code>, <code>wit</code> 和 <code>cum</code>. 我们通过把 <code>enum𝓕</code> 放到括号 <code>⦃ ⦄</code> 中来声明它是我们唯一谈论的那个 <code>Enum 𝓕</code>. 对于 <code>enum𝓡</code> 也一样.</p>
<p><strong><u>定理</u></strong> <code>𝓕</code> 和 <code>𝓡</code> 都是可数集.</p>
<pre class="Agda">  <a id="Language.count𝓕"></a><a id="1488" href="FOL.Language.html#1488" class="Function">count𝓕</a> <a id="1495" class="Symbol">:</a> <a id="1497" href="Foundation.Relation.Unary.Countable.html#164" class="Function">countable</a> <a id="1507" href="FOL.Language.html#825" class="Field">𝓕</a>
  <a id="1511" href="FOL.Language.html#1488" class="Function">count𝓕</a> <a id="1518" class="Symbol">=</a> <a id="1520" href="Foundation.Function.Enumeration.ListView.Properties.html#5775" class="Function">discr→enum→count</a> <a id="1537" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1539" href="FOL.Language.html#937" class="Field">enum𝓕</a> <a id="1545" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a>

  <a id="Language.count𝓡"></a><a id="1551" href="FOL.Language.html#1551" class="Function">count𝓡</a> <a id="1558" class="Symbol">:</a> <a id="1560" href="Foundation.Relation.Unary.Countable.html#164" class="Function">countable</a> <a id="1570" href="FOL.Language.html#835" class="Field">𝓡</a>
  <a id="1574" href="FOL.Language.html#1551" class="Function">count𝓡</a> <a id="1581" class="Symbol">=</a> <a id="1583" href="Foundation.Function.Enumeration.ListView.Properties.html#5775" class="Function">discr→enum→count</a> <a id="1600" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1602" href="FOL.Language.html#960" class="Field">enum𝓡</a> <a id="1608" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a>

  <a id="Language.isSet𝓕"></a><a id="1614" href="FOL.Language.html#1614" class="Function">isSet𝓕</a> <a id="1621" class="Symbol">:</a> <a id="1623" href="Foundation.Prelude.HLevel.html#889" class="Function">isSet</a> <a id="1629" href="FOL.Language.html#825" class="Field">𝓕</a>
  <a id="1633" href="FOL.Language.html#1614" class="Function">isSet𝓕</a> <a id="1640" class="Symbol">=</a> <a id="1642" href="Foundation.Relation.Nullary.Discrete.Base.html#728" class="Function">discreteSet</a>

  <a id="Language.isSet𝓡"></a><a id="1657" href="FOL.Language.html#1657" class="Function">isSet𝓡</a> <a id="1664" class="Symbol">:</a> <a id="1666" href="Foundation.Prelude.HLevel.html#889" class="Function">isSet</a> <a id="1672" href="FOL.Language.html#835" class="Field">𝓡</a>
  <a id="1676" href="FOL.Language.html#1657" class="Function">isSet𝓡</a> <a id="1683" class="Symbol">=</a> <a id="1685" href="Foundation.Relation.Nullary.Discrete.Base.html#728" class="Function">discreteSet</a>
</pre>
<p><strong><u>例</u></strong> 下面给出了语言的一个实例 <code>ℒ</code>, 它可以作为皮亚诺算术的语言.</p>
<pre class="Agda"><a id="1757" class="Keyword">private</a> <a id="1765" class="Keyword">module</a> <a id="ExampleLanguagePA"></a><a id="1772" href="FOL.Language.html#1772" class="Module">ExampleLanguagePA</a> <a id="1790" class="Keyword">where</a>

  <a id="1799" class="Keyword">data</a> <a id="ExampleLanguagePA.𝓕"></a><a id="1804" href="FOL.Language.html#1804" class="Datatype">𝓕</a> <a id="1806" class="Symbol">:</a> <a id="1808" href="Agda.Primitive.html#388" class="Primitive">𝕋</a> <a id="1810" class="Keyword">where</a>
    <a id="ExampleLanguagePA.𝓕.O"></a><a id="1820" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="1822" class="Symbol">:</a> <a id="1824" href="FOL.Language.html#1804" class="Datatype">𝓕</a>
    <a id="ExampleLanguagePA.𝓕.S"></a><a id="1830" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="1832" class="Symbol">:</a> <a id="1834" href="FOL.Language.html#1804" class="Datatype">𝓕</a>
    <a id="ExampleLanguagePA.𝓕.+"></a><a id="1840" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="1842" class="Symbol">:</a> <a id="1844" href="FOL.Language.html#1804" class="Datatype">𝓕</a>
    <a id="ExampleLanguagePA.𝓕.*"></a><a id="1850" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="1852" class="Symbol">:</a> <a id="1854" href="FOL.Language.html#1804" class="Datatype">𝓕</a>

  <a id="1859" class="Keyword">data</a> <a id="ExampleLanguagePA.𝓡"></a><a id="1864" href="FOL.Language.html#1864" class="Datatype">𝓡</a> <a id="1866" class="Symbol">:</a> <a id="1868" href="Agda.Primitive.html#388" class="Primitive">𝕋</a> <a id="1870" class="Keyword">where</a>
    <a id="ExampleLanguagePA.𝓡.&lt;"></a><a id="1880" href="FOL.Language.html#1880" class="InductiveConstructor">&lt;</a> <a id="1882" class="Symbol">:</a> <a id="1884" href="FOL.Language.html#1864" class="Datatype">𝓡</a>

  <a id="ExampleLanguagePA.∣_∣ᶠ"></a><a id="1889" href="FOL.Language.html#1889" class="Function Operator">∣_∣ᶠ</a> <a id="1894" class="Symbol">:</a> <a id="1896" href="FOL.Language.html#1804" class="Datatype">𝓕</a> <a id="1898" class="Symbol">→</a> <a id="1900" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
  <a id="1904" href="FOL.Language.html#1889" class="Function Operator">∣</a> <a id="1906" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="1908" href="FOL.Language.html#1889" class="Function Operator">∣ᶠ</a> <a id="1911" class="Symbol">=</a> <a id="1913" class="Number">0</a>
  <a id="1917" href="FOL.Language.html#1889" class="Function Operator">∣</a> <a id="1919" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="1921" href="FOL.Language.html#1889" class="Function Operator">∣ᶠ</a> <a id="1924" class="Symbol">=</a> <a id="1926" class="Number">1</a>
  <a id="1930" href="FOL.Language.html#1889" class="Function Operator">∣</a> <a id="1932" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="1934" href="FOL.Language.html#1889" class="Function Operator">∣ᶠ</a> <a id="1937" class="Symbol">=</a> <a id="1939" class="Number">2</a>
  <a id="1943" href="FOL.Language.html#1889" class="Function Operator">∣</a> <a id="1945" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="1947" href="FOL.Language.html#1889" class="Function Operator">∣ᶠ</a> <a id="1950" class="Symbol">=</a> <a id="1952" class="Number">2</a>

  <a id="ExampleLanguagePA.∣_∣ᴿ"></a><a id="1957" href="FOL.Language.html#1957" class="Function Operator">∣_∣ᴿ</a> <a id="1962" class="Symbol">:</a> <a id="1964" href="FOL.Language.html#1864" class="Datatype">𝓡</a> <a id="1966" class="Symbol">→</a> <a id="1968" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
  <a id="1972" href="FOL.Language.html#1957" class="Function Operator">∣</a> <a id="1974" href="FOL.Language.html#1880" class="InductiveConstructor">&lt;</a> <a id="1976" href="FOL.Language.html#1957" class="Function Operator">∣ᴿ</a> <a id="1979" class="Symbol">=</a> <a id="1981" class="Number">2</a>
</pre>
<p>通过模式匹配不难证明归纳定义的 <code>𝓕</code> 和 <code>𝓡</code> 是离散且可枚举的.</p>
<pre class="Agda">  <a id="ExampleLanguagePA.discr𝓕"></a><a id="2035" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2042" class="Symbol">:</a> <a id="2044" href="Relation.Binary.Definitions.html#6468" class="Function">discreteⓂ</a> <a id="2054" href="FOL.Language.html#1804" class="Datatype">𝓕</a>
  <a id="2058" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2065" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2067" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2069" class="Symbol">=</a> <a id="2071" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2075" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>
  <a id="2082" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2089" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2091" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2093" class="Symbol">=</a> <a id="2095" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2099" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>
  <a id="2106" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2113" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2115" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2117" class="Symbol">=</a> <a id="2119" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2123" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>
  <a id="2130" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2137" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2139" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2141" class="Symbol">=</a> <a id="2143" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2147" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>
  <a id="2154" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2161" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2163" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2165" class="Symbol">=</a> <a id="2167" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2170" class="Symbol">λ</a> <a id="2172" class="Symbol">()</a>
  <a id="2177" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2184" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2186" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2188" class="Symbol">=</a> <a id="2190" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2193" class="Symbol">λ</a> <a id="2195" class="Symbol">()</a>
  <a id="2200" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2207" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2209" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2211" class="Symbol">=</a> <a id="2213" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2216" class="Symbol">λ</a> <a id="2218" class="Symbol">()</a>
  <a id="2223" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2230" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2232" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2234" class="Symbol">=</a> <a id="2236" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2239" class="Symbol">λ</a> <a id="2241" class="Symbol">()</a>
  <a id="2246" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2253" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2255" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2257" class="Symbol">=</a> <a id="2259" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2262" class="Symbol">λ</a> <a id="2264" class="Symbol">()</a>
  <a id="2269" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2276" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2278" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2280" class="Symbol">=</a> <a id="2282" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2285" class="Symbol">λ</a> <a id="2287" class="Symbol">()</a>
  <a id="2292" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2299" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2301" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2303" class="Symbol">=</a> <a id="2305" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2308" class="Symbol">λ</a> <a id="2310" class="Symbol">()</a>
  <a id="2315" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2322" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2324" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2326" class="Symbol">=</a> <a id="2328" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2331" class="Symbol">λ</a> <a id="2333" class="Symbol">()</a>
  <a id="2338" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2345" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2347" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2349" class="Symbol">=</a> <a id="2351" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2354" class="Symbol">λ</a> <a id="2356" class="Symbol">()</a>
  <a id="2361" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2368" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2370" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2372" class="Symbol">=</a> <a id="2374" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2377" class="Symbol">λ</a> <a id="2379" class="Symbol">()</a>
  <a id="2384" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2391" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2393" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2395" class="Symbol">=</a> <a id="2397" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2400" class="Symbol">λ</a> <a id="2402" class="Symbol">()</a>
  <a id="2407" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2414" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2416" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2418" class="Symbol">=</a> <a id="2420" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2423" class="Symbol">λ</a> <a id="2425" class="Symbol">()</a>

  <a id="ExampleLanguagePA.discr𝓡"></a><a id="2431" href="FOL.Language.html#2431" class="Function">discr𝓡</a> <a id="2438" class="Symbol">:</a> <a id="2440" href="Relation.Binary.Definitions.html#6468" class="Function">discreteⓂ</a> <a id="2450" href="FOL.Language.html#1864" class="Datatype">𝓡</a>
  <a id="2454" href="FOL.Language.html#2431" class="Function">discr𝓡</a> <a id="2461" href="FOL.Language.html#1880" class="InductiveConstructor">&lt;</a> <a id="2463" href="FOL.Language.html#1880" class="InductiveConstructor">&lt;</a> <a id="2465" class="Symbol">=</a> <a id="2467" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2471" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>

  <a id="ExampleLanguagePA.enum𝓕"></a><a id="2479" href="FOL.Language.html#2479" class="Function">enum𝓕</a> <a id="2485" class="Symbol">:</a> <a id="2487" href="Foundation.Function.Enumeration.ListView.Base.html#2155" class="Record">Enum</a> <a id="2492" href="FOL.Language.html#1804" class="Datatype">𝓕</a>
  <a id="2496" href="FOL.Language.html#2479" class="Function">enum𝓕</a> <a id="2502" class="Symbol">=</a> <a id="2504" href="Foundation.Function.Enumeration.ListView.Base.html#2200" class="InductiveConstructor">mkEnum</a> <a id="2511" class="Symbol">(λ</a> <a id="2514" href="FOL.Language.html#2514" class="Bound">_</a> <a id="2516" class="Symbol">→</a> <a id="2518" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2520" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2522" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2524" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2526" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2528" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2530" href="Data.List.Base.html#5240" class="Function Operator">[</a> <a id="2532" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2534" href="Data.List.Base.html#5240" class="Function Operator">]</a><a id="2535" class="Symbol">)</a> <a id="2537" class="Symbol">(λ</a> <a id="2540" href="FOL.Language.html#2540" class="Bound">_</a> <a id="2542" class="Symbol">→</a> <a id="2544" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a> <a id="2547" href="Foundation.Prelude.Builtin.html#779" class="InductiveConstructor Operator">,</a> <a id="2549" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2553" class="Symbol">)</a>
    <a id="2559" class="Symbol">λ</a> <a id="2561" class="Symbol">{</a> <a id="2563" href="FOL.Language.html#1820" class="InductiveConstructor">O</a> <a id="2565" class="Symbol">→</a> <a id="2567" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2570" class="Number">0</a> <a id="2572" class="Symbol">(</a><a id="2573" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2578" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2582" class="Symbol">)</a>
      <a id="2590" class="Symbol">;</a> <a id="2592" href="FOL.Language.html#1830" class="InductiveConstructor">S</a> <a id="2594" class="Symbol">→</a> <a id="2596" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2599" class="Number">0</a> <a id="2601" class="Symbol">(</a><a id="2602" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2608" class="Symbol">(</a><a id="2609" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2614" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2618" class="Symbol">))</a>
      <a id="2627" class="Symbol">;</a> <a id="2629" href="FOL.Language.html#1840" class="InductiveConstructor">+</a> <a id="2631" class="Symbol">→</a> <a id="2633" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2636" class="Number">0</a> <a id="2638" class="Symbol">(</a><a id="2639" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2645" class="Symbol">(</a><a id="2646" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2652" class="Symbol">(</a><a id="2653" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2658" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2662" class="Symbol">)))</a>
      <a id="2672" class="Symbol">;</a> <a id="2674" href="FOL.Language.html#1850" class="InductiveConstructor">*</a> <a id="2676" class="Symbol">→</a> <a id="2678" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2681" class="Number">0</a> <a id="2683" class="Symbol">(</a><a id="2684" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2690" class="Symbol">(</a><a id="2691" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2697" class="Symbol">(</a><a id="2698" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2704" class="Symbol">(</a><a id="2705" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2710" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2714" class="Symbol">))))</a> <a id="2719" class="Symbol">}</a>

  <a id="ExampleLanguagePA.enum𝓡"></a><a id="2724" href="FOL.Language.html#2724" class="Function">enum𝓡</a> <a id="2730" class="Symbol">:</a> <a id="2732" href="Foundation.Function.Enumeration.ListView.Base.html#2155" class="Record">Enum</a> <a id="2737" href="FOL.Language.html#1864" class="Datatype">𝓡</a>
  <a id="2741" href="FOL.Language.html#2724" class="Function">enum𝓡</a> <a id="2747" class="Symbol">=</a> <a id="2749" href="Foundation.Function.Enumeration.ListView.Base.html#2200" class="InductiveConstructor">mkEnum</a> <a id="2756" class="Symbol">(λ</a> <a id="2759" href="FOL.Language.html#2759" class="Bound">_</a> <a id="2761" class="Symbol">→</a> <a id="2763" href="FOL.Language.html#1880" class="InductiveConstructor">&lt;</a> <a id="2765" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2767" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="2769" class="Symbol">)</a> <a id="2771" class="Symbol">(λ</a> <a id="2774" href="FOL.Language.html#2774" class="Bound">_</a> <a id="2776" class="Symbol">→</a> <a id="2778" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a> <a id="2781" href="Foundation.Prelude.Builtin.html#779" class="InductiveConstructor Operator">,</a> <a id="2783" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2787" class="Symbol">)</a>
    <a id="2793" class="Symbol">λ</a> <a id="2795" class="Symbol">{</a> <a id="2797" href="FOL.Language.html#1880" class="InductiveConstructor">&lt;</a> <a id="2799" class="Symbol">→</a> <a id="2801" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2804" class="Number">0</a> <a id="2806" class="Symbol">(</a><a id="2807" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2812" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2816" class="Symbol">)</a> <a id="2818" class="Symbol">}</a>
</pre>
<p>于是它们可以构成语言.</p>
<pre class="Agda">  <a id="ExampleLanguagePA.ℒ"></a><a id="2848" href="FOL.Language.html#2848" class="Function">ℒ</a> <a id="2850" class="Symbol">:</a> <a id="2852" href="FOL.Language.html#772" class="Record">Language</a>
  <a id="2863" href="FOL.Language.html#2848" class="Function">ℒ</a> <a id="2865" class="Symbol">=</a> <a id="2867" class="Keyword">record</a>
    <a id="2878" class="Symbol">{</a> <a id="2880" href="FOL.Language.html#825" class="Field">𝓕</a> <a id="2882" class="Symbol">=</a> <a id="2884" href="FOL.Language.html#1804" class="Datatype">𝓕</a>
    <a id="2890" class="Symbol">;</a> <a id="2892" href="FOL.Language.html#835" class="Field">𝓡</a> <a id="2894" class="Symbol">=</a> <a id="2896" href="FOL.Language.html#1864" class="Datatype">𝓡</a>
    <a id="2902" class="Symbol">;</a> <a id="2904" href="FOL.Language.html#845" class="Field Operator">∣_∣ᶠ</a> <a id="2909" class="Symbol">=</a> <a id="2911" href="FOL.Language.html#1889" class="Function Operator">∣_∣ᶠ</a>
    <a id="2920" class="Symbol">;</a> <a id="2922" href="FOL.Language.html#862" class="Field Operator">∣_∣ᴿ</a> <a id="2927" class="Symbol">=</a> <a id="2929" href="FOL.Language.html#1957" class="Function Operator">∣_∣ᴿ</a>
    <a id="2938" class="Symbol">;</a> <a id="2940" href="FOL.Language.html#881" class="Field">discr𝓕</a> <a id="2947" class="Symbol">=</a> <a id="2949" href="FOL.Language.html#2035" class="Function">discr𝓕</a> <a id="2956" class="Symbol">_</a> <a id="2958" class="Symbol">_</a>
    <a id="2964" class="Symbol">;</a> <a id="2966" href="FOL.Language.html#909" class="Field">discr𝓡</a> <a id="2973" class="Symbol">=</a> <a id="2975" href="FOL.Language.html#2431" class="Function">discr𝓡</a> <a id="2982" class="Symbol">_</a> <a id="2984" class="Symbol">_</a>
    <a id="2990" class="Symbol">;</a> <a id="2992" href="FOL.Language.html#937" class="Field">enum𝓕</a> <a id="2998" class="Symbol">=</a> <a id="3000" href="FOL.Language.html#2479" class="Function">enum𝓕</a>
    <a id="3010" class="Symbol">;</a> <a id="3012" href="FOL.Language.html#960" class="Field">enum𝓡</a> <a id="3018" class="Symbol">=</a> <a id="3020" href="FOL.Language.html#2724" class="Function">enum𝓡</a>
    <a id="3030" class="Symbol">}</a>
</pre>
<p><strong><u>约定</u></strong> 我们一次只会谈论一种语言, 它在上下文中是明确的, 首次出现时会放在括号 <code>⦃ ⦄</code> 中或使用 <code>instance</code> 关键字来声明, 所以每次提到语言中的概念时不会一一带上某语言 <code>ℒ</code> 作为参数, 从而精简表达. 该约定表达为以下代码.</p>
<pre class="Agda"><a id="3180" class="Keyword">open</a> <a id="3185" href="FOL.Language.html#772" class="Module">Language</a> <a id="3194" class="Symbol">⦃...⦄</a> <a id="3200" class="Keyword">public</a>
</pre>
<hr />
<blockquote>
<p>知识共享许可协议: <a href="https://creativecommons.org/licenses/by-nc-sa/4.0/deed.zh">CC-BY-NC-SA 4.0</a><br />
<a href="https://github.com/choukh/MetaLogic/blob/main/src/FOL/Language.lagda.md">GitHub</a> | <a href="https://choukh.github.io/MetaLogic/FOL.Language.html">GitHub Pages</a> | <a href="https://www.yuque.com/ocau/metalogic/fol.language">语雀</a><br />
交流Q群: 893531731</p>
</blockquote>
</main>

<script>
;(function() {
  // Non-essential if user has JavaScript off. Just makes checkboxes look nicer.
  var selector = '.task-list > li > input[type="checkbox"]';
  var checkboxes = document.querySelectorAll(selector);
  Array.from(checkboxes).forEach((checkbox) => {
    var wasChecked = checkbox.checked;
    checkbox.disabled = false;
    checkbox.addEventListener('click', (ev) => {ev.target.checked = wasChecked});
  });
})();
</script>
</body>
</html>
