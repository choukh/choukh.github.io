<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc-markdown-css-theme" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <title>FOL.Language</title>
  <link rel="stylesheet" href="css/theme.css" />
  <link rel="stylesheet" href="css/skylighting-solarized-theme.css" />
  <script type="text/javascript" src="highlight-hover.js"></script>
  <script defer="" src="https://cdn.jsdelivr.net/npm/katex@0.15.1/dist/katex.min.js"></script>
  <script>document.addEventListener("DOMContentLoaded", function () {
 var mathElements = document.getElementsByClassName("math");
 var macros = [];
 for (var i = 0; i < mathElements.length; i++) {
  var texText = mathElements[i].firstChild;
  if (mathElements[i].tagName == "SPAN") {
   katex.render(texText.data, mathElements[i], {
    displayMode: mathElements[i].classList.contains('display'),
    throwOnError: false,
    macros: macros,
    fleqn: false
   });
}}});
  </script>
  <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/katex@0.15.1/dist/katex.min.css" />
</head>
<body>

<header>
<h1 class="title"></h1>
<blockquote class="metadata">
</blockquote>
</header>

<nav id="TOC" role="doc-toc">
    <strong>Contents</strong><label for="contents">⊕</label>
  <input type="checkbox" id="contents">
  <ul>
  <li><a href="#一阶逻辑-语言" id="toc-一阶逻辑-语言">一阶逻辑 ▸ 语言</a></li>
  </ul>
</nav>

<main>
<h1 id="一阶逻辑-语言">一阶逻辑 ▸ 语言</h1>
<p>一阶逻辑是一种形式语言, 其语句由一些原始符号按一定的语法组合而成. 符号又分为逻辑符号和非逻辑符号. 本篇先讲非逻辑符号.</p>
<p>非逻辑符号分为函数符号和关系符号, 且它们都带有一个称为元数 (arity) 的属性. 例如, 元数为 2 的函数符号即用于表示二元函数. 特别地, 元数为零的函数又称为常量.</p>
<p>较传统的处理方式是给出所有可能的函数符号和关系符号. 即对任意元数 <span class="math inline">n</span>, 都有自然数多个函数符号</p>
<p><span class="math display">f^n_0,\ f^n_1,\ f^n_2,\ f^n_3,\ ...</span></p>
<p>以及自然数多个关系符号</p>
<p><span class="math display">R^n_0,\ R^n_1,\ R^n_2,\ R^n_3,\ ...</span></p>
<p>在这种处理下, 只有唯一一种一阶逻辑语言.</p>
<p>较现代的方式是根据最终要实现的一阶逻辑语言来指定该理论所需的非逻辑符号. 这些特定的符号以及它们的元数所组成的资料叫做理论的<strong>签名 (signature)</strong>. 在这种处理下, 每种签名都对应一种一阶逻辑语言, 因此签名又叫做<strong>语言 (language)</strong>, 语言的实例按惯例记作 ℒ. 由于一阶逻辑的其他部分都是参数化到语言的, 我们把它单独作为一个模块.</p>
<pre class="Agda"><a id="557" class="Comment">-- 元语言的基本概念</a>
<a id="569" class="Keyword">open</a> <a id="574" class="Keyword">import</a> <a id="581" href="Foundation.Essential.html" class="Module">Foundation.Essential</a>
<a id="602" class="Comment">-- 可枚举性的相关概念</a>
<a id="615" class="Keyword">open</a> <a id="620" class="Keyword">import</a> <a id="627" href="Enumerability.View.Lists.html" class="Module">Enumerability.View.Lists</a>

<a id="653" class="Keyword">module</a> <a id="660" href="FOL.Language.html" class="Module">FOL.Language</a> <a id="673" class="Keyword">where</a>
</pre>
<p><strong><u>定义</u></strong> 一阶逻辑的语言 <code>ℒ</code> 是一个六元组</p>
<ol type="1">
<li>离散的函数符号集 <code>𝓕</code></li>
<li>离散的关系符号集 <code>𝓡</code></li>
<li><code>𝓕</code> 到元数的映射 <code>∣_∣ᶠ</code></li>
<li><code>𝓡</code> 到元数的映射 <code>∣_∣ᴿ</code></li>
<li><code>𝓕</code> 的一个枚举</li>
<li><code>𝓡</code> 的一个枚举</li>
</ol>
<pre class="Agda"><a id="827" class="Keyword">record</a> <a id="Language"></a><a id="834" href="FOL.Language.html#834" class="Record">Language</a> <a id="843" class="Symbol">:</a> <a id="845" href="Agda.Primitive.html#388" class="Primitive">𝕋₁</a> <a id="848" class="Keyword">where</a>
  <a id="856" class="Keyword">constructor</a> <a id="mkLang"></a><a id="868" href="FOL.Language.html#868" class="InductiveConstructor">mkLang</a>
  <a id="877" class="Keyword">field</a>
    <a id="Language.𝓕"></a><a id="887" href="FOL.Language.html#887" class="Field">𝓕</a> <a id="889" class="Symbol">:</a> <a id="891" href="Agda.Primitive.html#388" class="Primitive">𝕋</a>
    <a id="Language.𝓡"></a><a id="897" href="FOL.Language.html#897" class="Field">𝓡</a> <a id="899" class="Symbol">:</a> <a id="901" href="Agda.Primitive.html#388" class="Primitive">𝕋</a>
    <a id="Language.∣_∣ᶠ"></a><a id="907" href="FOL.Language.html#907" class="Field Operator">∣_∣ᶠ</a> <a id="912" class="Symbol">:</a> <a id="914" href="FOL.Language.html#887" class="Field">𝓕</a> <a id="916" class="Symbol">→</a> <a id="918" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
    <a id="Language.∣_∣ᴿ"></a><a id="924" href="FOL.Language.html#924" class="Field Operator">∣_∣ᴿ</a> <a id="929" class="Symbol">:</a> <a id="931" href="FOL.Language.html#897" class="Field">𝓡</a> <a id="933" class="Symbol">→</a> <a id="935" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
    <a id="Language.discr𝓡"></a><a id="941" href="FOL.Language.html#941" class="Field">discr𝓡</a> <a id="948" class="Symbol">:</a> <a id="950" href="Relation.Binary.Definitions.html#6468" class="Function">discrete</a> <a id="959" href="FOL.Language.html#897" class="Field">𝓡</a>
    <a id="Language.discr𝓕"></a><a id="965" href="FOL.Language.html#965" class="Field">discr𝓕</a> <a id="972" class="Symbol">:</a> <a id="974" href="Relation.Binary.Definitions.html#6468" class="Function">discrete</a> <a id="983" href="FOL.Language.html#887" class="Field">𝓕</a>
    <a id="989" class="Symbol">⦃</a> <a id="Language.enum𝓕"></a><a id="991" href="FOL.Language.html#991" class="Field">enum𝓕</a> <a id="997" class="Symbol">⦄</a> <a id="999" class="Symbol">:</a> <a id="1001" href="Enumerability.View.Lists.html#1418" class="Record">Enum</a> <a id="1006" href="FOL.Language.html#887" class="Field">𝓕</a>
    <a id="1012" class="Symbol">⦃</a> <a id="Language.enum𝓡"></a><a id="1014" href="FOL.Language.html#1014" class="Field">enum𝓡</a> <a id="1020" class="Symbol">⦄</a> <a id="1022" class="Symbol">:</a> <a id="1024" href="Enumerability.View.Lists.html#1418" class="Record">Enum</a> <a id="1029" href="FOL.Language.html#897" class="Field">𝓡</a>
</pre>
<p><strong><u>注意</u></strong> 在经典语境下集合一定是离散的, 但在直觉主义 HoTT 中, 离散强于“集合”. 因此当我们要求某 <code>A</code> 是“离散集”的时候, 实际上只要求它是离散类型, 然后它自然是一个集合.</p>
<p><strong><u>注意</u></strong> 回顾<a href="https://www.yuque.com/ocau/metalogic/foundation.essential#c1933822">枚举的定义</a>, 某类型 <code>A</code> 的枚举 <code>Enum A</code>是一个二元组: 1. 满足 <code>wit</code> 的一个函数 <code>enum</code> 2. <code>enum</code> 的一个 <code>cum</code></p>
<p><strong><u>约定</u></strong> 对于一个类型, 我们自始至终只会谈论它的一个枚举. 所以对任意类型的枚举, 我们都会用 <code>enum</code>, <code>wit</code> 和 <code>cum</code> 来指代该类型的那个我们唯一谈论的枚举的 <code>enum</code>, <code>wit</code> 和 <code>cum</code>. 我们通过把 <code>enum𝓕</code> 放到括号 <code>⦃ ⦄</code> 中来声明它是我们唯一谈论的那个 <code>Enum 𝓕</code>. 对于 <code>enum𝓡</code> 也一样.</p>
<p><strong><u>定理</u></strong> <code>𝓕</code> 和 <code>𝓡</code> 都是可数集.</p>
<pre class="Agda">  <a id="Language.count𝓕"></a><a id="1542" href="FOL.Language.html#1542" class="Function">count𝓕</a> <a id="1549" class="Symbol">:</a> <a id="1551" href="Foundation.Relation.Unary.Countable.html#164" class="Function">countable</a> <a id="1561" href="FOL.Language.html#887" class="Field">𝓕</a>
  <a id="1565" href="FOL.Language.html#1542" class="Function">count𝓕</a> <a id="1572" class="Symbol">=</a> <a id="1574" href="Enumerability.View.Lists.html#8122" class="Function">discr→enum→count</a> <a id="1591" href="FOL.Language.html#965" class="Field">discr𝓕</a> <a id="1598" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1600" href="FOL.Language.html#991" class="Field">enum𝓕</a> <a id="1606" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a>

  <a id="Language.count𝓡"></a><a id="1612" href="FOL.Language.html#1612" class="Function">count𝓡</a> <a id="1619" class="Symbol">:</a> <a id="1621" href="Foundation.Relation.Unary.Countable.html#164" class="Function">countable</a> <a id="1631" href="FOL.Language.html#897" class="Field">𝓡</a>
  <a id="1635" href="FOL.Language.html#1612" class="Function">count𝓡</a> <a id="1642" class="Symbol">=</a> <a id="1644" href="Enumerability.View.Lists.html#8122" class="Function">discr→enum→count</a> <a id="1661" href="FOL.Language.html#941" class="Field">discr𝓡</a> <a id="1668" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣</a> <a id="1670" href="FOL.Language.html#1014" class="Field">enum𝓡</a> <a id="1676" href="Cubical.HITs.PropositionalTruncation.Base.html#288" class="InductiveConstructor Operator">∣₁</a>

  <a id="Language.isSet𝓕"></a><a id="1682" href="FOL.Language.html#1682" class="Function">isSet𝓕</a> <a id="1689" class="Symbol">:</a> <a id="1691" href="Foundation.Prelude.HLevel.html#889" class="Function">isSet</a> <a id="1697" href="FOL.Language.html#887" class="Field">𝓕</a>
  <a id="1701" href="FOL.Language.html#1682" class="Function">isSet𝓕</a> <a id="1708" class="Symbol">=</a> <a id="1710" href="Foundation.Relation.Nullary.Discrete.html#547" class="Function">discrete→isSet</a> <a id="1725" href="FOL.Language.html#965" class="Field">discr𝓕</a>

  <a id="Language.isSet𝓡"></a><a id="1735" href="FOL.Language.html#1735" class="Function">isSet𝓡</a> <a id="1742" class="Symbol">:</a> <a id="1744" href="Foundation.Prelude.HLevel.html#889" class="Function">isSet</a> <a id="1750" href="FOL.Language.html#897" class="Field">𝓡</a>
  <a id="1754" href="FOL.Language.html#1735" class="Function">isSet𝓡</a> <a id="1761" class="Symbol">=</a> <a id="1763" href="Foundation.Relation.Nullary.Discrete.html#547" class="Function">discrete→isSet</a> <a id="1778" href="FOL.Language.html#941" class="Field">discr𝓡</a>
</pre>
<p><strong><u>例</u></strong> 下面给出了语言的一个实例 <code>ℒ</code>, 它可以作为皮亚诺算术的语言.</p>
<pre class="Agda"><a id="1845" class="Keyword">private</a> <a id="1853" class="Keyword">module</a> <a id="ExampleLanguagePA"></a><a id="1860" href="FOL.Language.html#1860" class="Module">ExampleLanguagePA</a> <a id="1878" class="Keyword">where</a>

  <a id="1887" class="Keyword">data</a> <a id="ExampleLanguagePA.𝓕"></a><a id="1892" href="FOL.Language.html#1892" class="Datatype">𝓕</a> <a id="1894" class="Symbol">:</a> <a id="1896" href="Agda.Primitive.html#388" class="Primitive">𝕋</a> <a id="1898" class="Keyword">where</a>
    <a id="ExampleLanguagePA.𝓕.O"></a><a id="1908" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="1910" class="Symbol">:</a> <a id="1912" href="FOL.Language.html#1892" class="Datatype">𝓕</a>
    <a id="ExampleLanguagePA.𝓕.S"></a><a id="1918" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="1920" class="Symbol">:</a> <a id="1922" href="FOL.Language.html#1892" class="Datatype">𝓕</a>
    <a id="ExampleLanguagePA.𝓕.+"></a><a id="1928" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="1930" class="Symbol">:</a> <a id="1932" href="FOL.Language.html#1892" class="Datatype">𝓕</a>
    <a id="ExampleLanguagePA.𝓕.*"></a><a id="1938" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="1940" class="Symbol">:</a> <a id="1942" href="FOL.Language.html#1892" class="Datatype">𝓕</a>

  <a id="1947" class="Keyword">data</a> <a id="ExampleLanguagePA.𝓡"></a><a id="1952" href="FOL.Language.html#1952" class="Datatype">𝓡</a> <a id="1954" class="Symbol">:</a> <a id="1956" href="Agda.Primitive.html#388" class="Primitive">𝕋</a> <a id="1958" class="Keyword">where</a>
    <a id="ExampleLanguagePA.𝓡.&lt;"></a><a id="1968" href="FOL.Language.html#1968" class="InductiveConstructor">&lt;</a> <a id="1970" class="Symbol">:</a> <a id="1972" href="FOL.Language.html#1952" class="Datatype">𝓡</a>

  <a id="ExampleLanguagePA.∣_∣ᶠ"></a><a id="1977" href="FOL.Language.html#1977" class="Function Operator">∣_∣ᶠ</a> <a id="1982" class="Symbol">:</a> <a id="1984" href="FOL.Language.html#1892" class="Datatype">𝓕</a> <a id="1986" class="Symbol">→</a> <a id="1988" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
  <a id="1992" href="FOL.Language.html#1977" class="Function Operator">∣</a> <a id="1994" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="1996" href="FOL.Language.html#1977" class="Function Operator">∣ᶠ</a> <a id="1999" class="Symbol">=</a> <a id="2001" class="Number">0</a>
  <a id="2005" href="FOL.Language.html#1977" class="Function Operator">∣</a> <a id="2007" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2009" href="FOL.Language.html#1977" class="Function Operator">∣ᶠ</a> <a id="2012" class="Symbol">=</a> <a id="2014" class="Number">1</a>
  <a id="2018" href="FOL.Language.html#1977" class="Function Operator">∣</a> <a id="2020" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2022" href="FOL.Language.html#1977" class="Function Operator">∣ᶠ</a> <a id="2025" class="Symbol">=</a> <a id="2027" class="Number">2</a>
  <a id="2031" href="FOL.Language.html#1977" class="Function Operator">∣</a> <a id="2033" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2035" href="FOL.Language.html#1977" class="Function Operator">∣ᶠ</a> <a id="2038" class="Symbol">=</a> <a id="2040" class="Number">2</a>

  <a id="ExampleLanguagePA.∣_∣ᴿ"></a><a id="2045" href="FOL.Language.html#2045" class="Function Operator">∣_∣ᴿ</a> <a id="2050" class="Symbol">:</a> <a id="2052" href="FOL.Language.html#1952" class="Datatype">𝓡</a> <a id="2054" class="Symbol">→</a> <a id="2056" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
  <a id="2060" href="FOL.Language.html#2045" class="Function Operator">∣</a> <a id="2062" href="FOL.Language.html#1968" class="InductiveConstructor">&lt;</a> <a id="2064" href="FOL.Language.html#2045" class="Function Operator">∣ᴿ</a> <a id="2067" class="Symbol">=</a> <a id="2069" class="Number">2</a>
</pre>
<p>通过模式匹配不难证明归纳定义的 <code>𝓕</code> 和 <code>𝓡</code> 是离散且可枚举的.</p>
<pre class="Agda">  <a id="ExampleLanguagePA.discr𝓕"></a><a id="2123" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2130" class="Symbol">:</a> <a id="2132" href="Relation.Binary.Definitions.html#6468" class="Function">discrete</a> <a id="2141" href="FOL.Language.html#1892" class="Datatype">𝓕</a>
  <a id="2145" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2152" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2154" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2156" class="Symbol">=</a> <a id="2158" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2162" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>
  <a id="2169" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2176" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2178" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2180" class="Symbol">=</a> <a id="2182" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2186" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>
  <a id="2193" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2200" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2202" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2204" class="Symbol">=</a> <a id="2206" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2210" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>
  <a id="2217" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2224" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2226" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2228" class="Symbol">=</a> <a id="2230" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2234" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>
  <a id="2241" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2248" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2250" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2252" class="Symbol">=</a> <a id="2254" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2257" class="Symbol">λ</a> <a id="2259" class="Symbol">()</a>
  <a id="2264" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2271" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2273" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2275" class="Symbol">=</a> <a id="2277" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2280" class="Symbol">λ</a> <a id="2282" class="Symbol">()</a>
  <a id="2287" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2294" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2296" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2298" class="Symbol">=</a> <a id="2300" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2303" class="Symbol">λ</a> <a id="2305" class="Symbol">()</a>
  <a id="2310" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2317" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2319" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2321" class="Symbol">=</a> <a id="2323" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2326" class="Symbol">λ</a> <a id="2328" class="Symbol">()</a>
  <a id="2333" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2340" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2342" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2344" class="Symbol">=</a> <a id="2346" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2349" class="Symbol">λ</a> <a id="2351" class="Symbol">()</a>
  <a id="2356" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2363" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2365" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2367" class="Symbol">=</a> <a id="2369" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2372" class="Symbol">λ</a> <a id="2374" class="Symbol">()</a>
  <a id="2379" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2386" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2388" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2390" class="Symbol">=</a> <a id="2392" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2395" class="Symbol">λ</a> <a id="2397" class="Symbol">()</a>
  <a id="2402" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2409" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2411" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2413" class="Symbol">=</a> <a id="2415" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2418" class="Symbol">λ</a> <a id="2420" class="Symbol">()</a>
  <a id="2425" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2432" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2434" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2436" class="Symbol">=</a> <a id="2438" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2441" class="Symbol">λ</a> <a id="2443" class="Symbol">()</a>
  <a id="2448" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2455" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2457" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2459" class="Symbol">=</a> <a id="2461" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2464" class="Symbol">λ</a> <a id="2466" class="Symbol">()</a>
  <a id="2471" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2478" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2480" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2482" class="Symbol">=</a> <a id="2484" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2487" class="Symbol">λ</a> <a id="2489" class="Symbol">()</a>
  <a id="2494" href="FOL.Language.html#2123" class="Function">discr𝓕</a> <a id="2501" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2503" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2505" class="Symbol">=</a> <a id="2507" href="Relation.Nullary.Decidable.Core.html#1648" class="InductiveConstructor">no</a> <a id="2510" class="Symbol">λ</a> <a id="2512" class="Symbol">()</a>

  <a id="ExampleLanguagePA.discr𝓡"></a><a id="2518" href="FOL.Language.html#2518" class="Function">discr𝓡</a> <a id="2525" class="Symbol">:</a> <a id="2527" href="Relation.Binary.Definitions.html#6468" class="Function">discrete</a> <a id="2536" href="FOL.Language.html#1952" class="Datatype">𝓡</a>
  <a id="2540" href="FOL.Language.html#2518" class="Function">discr𝓡</a> <a id="2547" href="FOL.Language.html#1968" class="InductiveConstructor">&lt;</a> <a id="2549" href="FOL.Language.html#1968" class="InductiveConstructor">&lt;</a> <a id="2551" class="Symbol">=</a> <a id="2553" href="Relation.Nullary.Decidable.Core.html#1611" class="InductiveConstructor">yes</a> <a id="2557" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a>

  <a id="ExampleLanguagePA.enum𝓕"></a><a id="2565" href="FOL.Language.html#2565" class="Function">enum𝓕</a> <a id="2571" class="Symbol">:</a> <a id="2573" href="Enumerability.View.Lists.html#1418" class="Record">Enum</a> <a id="2578" href="FOL.Language.html#1892" class="Datatype">𝓕</a>
  <a id="2582" href="FOL.Language.html#2565" class="Function">enum𝓕</a> <a id="2588" class="Symbol">=</a> <a id="2590" href="Enumerability.View.Lists.html#1463" class="InductiveConstructor">mkEnum</a> <a id="2597" class="Symbol">(λ</a> <a id="2600" href="FOL.Language.html#2600" class="Bound">_</a> <a id="2602" class="Symbol">→</a> <a id="2604" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2606" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2608" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2610" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2612" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2614" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2616" href="Data.List.Base.html#5240" class="Function Operator">[</a> <a id="2618" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2620" href="Data.List.Base.html#5240" class="Function Operator">]</a><a id="2621" class="Symbol">)</a> <a id="2623" class="Symbol">(λ</a> <a id="2626" href="FOL.Language.html#2626" class="Bound">_</a> <a id="2628" class="Symbol">→</a> <a id="2630" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a> <a id="2633" href="Foundation.Prelude.Builtin.html#779" class="InductiveConstructor Operator">,</a> <a id="2635" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2639" class="Symbol">)</a>
    <a id="2645" class="Symbol">λ</a> <a id="2647" class="Symbol">{</a> <a id="2649" href="FOL.Language.html#1908" class="InductiveConstructor">O</a> <a id="2651" class="Symbol">→</a> <a id="2653" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2656" class="Number">0</a> <a id="2658" class="Symbol">(</a><a id="2659" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2664" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2668" class="Symbol">)</a>
      <a id="2676" class="Symbol">;</a> <a id="2678" href="FOL.Language.html#1918" class="InductiveConstructor">S</a> <a id="2680" class="Symbol">→</a> <a id="2682" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2685" class="Number">0</a> <a id="2687" class="Symbol">(</a><a id="2688" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2694" class="Symbol">(</a><a id="2695" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2700" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2704" class="Symbol">))</a>
      <a id="2713" class="Symbol">;</a> <a id="2715" href="FOL.Language.html#1928" class="InductiveConstructor">+</a> <a id="2717" class="Symbol">→</a> <a id="2719" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2722" class="Number">0</a> <a id="2724" class="Symbol">(</a><a id="2725" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2731" class="Symbol">(</a><a id="2732" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2738" class="Symbol">(</a><a id="2739" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2744" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2748" class="Symbol">)))</a>
      <a id="2758" class="Symbol">;</a> <a id="2760" href="FOL.Language.html#1938" class="InductiveConstructor">*</a> <a id="2762" class="Symbol">→</a> <a id="2764" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2767" class="Number">0</a> <a id="2769" class="Symbol">(</a><a id="2770" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2776" class="Symbol">(</a><a id="2777" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2783" class="Symbol">(</a><a id="2784" href="Data.List.Relation.Unary.Any.html#1279" class="InductiveConstructor">there</a> <a id="2790" class="Symbol">(</a><a id="2791" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2796" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2800" class="Symbol">))))</a> <a id="2805" class="Symbol">}</a>

  <a id="ExampleLanguagePA.enum𝓡"></a><a id="2810" href="FOL.Language.html#2810" class="Function">enum𝓡</a> <a id="2816" class="Symbol">:</a> <a id="2818" href="Enumerability.View.Lists.html#1418" class="Record">Enum</a> <a id="2823" href="FOL.Language.html#1952" class="Datatype">𝓡</a>
  <a id="2827" href="FOL.Language.html#2810" class="Function">enum𝓡</a> <a id="2833" class="Symbol">=</a> <a id="2835" href="Enumerability.View.Lists.html#1463" class="InductiveConstructor">mkEnum</a> <a id="2842" class="Symbol">(λ</a> <a id="2845" href="FOL.Language.html#2845" class="Bound">_</a> <a id="2847" class="Symbol">→</a> <a id="2849" href="FOL.Language.html#1968" class="InductiveConstructor">&lt;</a> <a id="2851" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="2853" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="2855" class="Symbol">)</a> <a id="2857" class="Symbol">(λ</a> <a id="2860" href="FOL.Language.html#2860" class="Bound">_</a> <a id="2862" class="Symbol">→</a> <a id="2864" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a> <a id="2867" href="Foundation.Prelude.Builtin.html#779" class="InductiveConstructor Operator">,</a> <a id="2869" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2873" class="Symbol">)</a>
    <a id="2879" class="Symbol">λ</a> <a id="2881" class="Symbol">{</a> <a id="2883" href="FOL.Language.html#1968" class="InductiveConstructor">&lt;</a> <a id="2885" class="Symbol">→</a> <a id="2887" href="Foundation.Prop.Logic.html#821" class="Function">ex</a> <a id="2890" class="Number">0</a> <a id="2892" class="Symbol">(</a><a id="2893" href="Data.List.Relation.Unary.Any.html#1226" class="InductiveConstructor">here</a> <a id="2898" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">refl</a><a id="2902" class="Symbol">)</a> <a id="2904" class="Symbol">}</a>
</pre>
<p>于是它们可以构成语言.</p>
<pre class="Agda">  <a id="ExampleLanguagePA.ℒ"></a><a id="2934" href="FOL.Language.html#2934" class="Function">ℒ</a> <a id="2936" class="Symbol">:</a> <a id="2938" href="FOL.Language.html#834" class="Record">Language</a>
  <a id="2949" href="FOL.Language.html#2934" class="Function">ℒ</a> <a id="2951" class="Symbol">=</a> <a id="2953" class="Keyword">record</a>
    <a id="2964" class="Symbol">{</a> <a id="2966" href="FOL.Language.html#887" class="Field">𝓕</a> <a id="2968" class="Symbol">=</a> <a id="2970" href="FOL.Language.html#1892" class="Datatype">𝓕</a>
    <a id="2976" class="Symbol">;</a> <a id="2978" href="FOL.Language.html#897" class="Field">𝓡</a> <a id="2980" class="Symbol">=</a> <a id="2982" href="FOL.Language.html#1952" class="Datatype">𝓡</a>
    <a id="2988" class="Symbol">;</a> <a id="2990" href="FOL.Language.html#907" class="Field Operator">∣_∣ᶠ</a> <a id="2995" class="Symbol">=</a> <a id="2997" href="FOL.Language.html#1977" class="Function Operator">∣_∣ᶠ</a>
    <a id="3006" class="Symbol">;</a> <a id="3008" href="FOL.Language.html#924" class="Field Operator">∣_∣ᴿ</a> <a id="3013" class="Symbol">=</a> <a id="3015" href="FOL.Language.html#2045" class="Function Operator">∣_∣ᴿ</a>
    <a id="3024" class="Symbol">;</a> <a id="3026" href="FOL.Language.html#965" class="Field">discr𝓕</a> <a id="3033" class="Symbol">=</a> <a id="3035" href="FOL.Language.html#2123" class="Function">discr𝓕</a>
    <a id="3046" class="Symbol">;</a> <a id="3048" href="FOL.Language.html#941" class="Field">discr𝓡</a> <a id="3055" class="Symbol">=</a> <a id="3057" href="FOL.Language.html#2518" class="Function">discr𝓡</a>
    <a id="3068" class="Symbol">;</a> <a id="3070" href="FOL.Language.html#991" class="Field">enum𝓕</a> <a id="3076" class="Symbol">=</a> <a id="3078" href="FOL.Language.html#2565" class="Function">enum𝓕</a>
    <a id="3088" class="Symbol">;</a> <a id="3090" href="FOL.Language.html#1014" class="Field">enum𝓡</a> <a id="3096" class="Symbol">=</a> <a id="3098" href="FOL.Language.html#2810" class="Function">enum𝓡</a>
    <a id="3108" class="Symbol">}</a>
</pre>
<p><strong><u>约定</u></strong> 我们一次只会谈论一种语言, 它在上下文中是明确的, 首次出现时会放在括号 <code>⦃ ⦄</code> 中或使用 <code>instance</code> 关键字来声明, 所以每次提到语言中的概念时不会一一带上某语言 <code>ℒ</code> 作为参数, 从而精简表达. 该约定表达为以下代码.</p>
<pre class="Agda"><a id="3258" class="Keyword">open</a> <a id="3263" href="FOL.Language.html#834" class="Module">Language</a> <a id="3272" class="Symbol">⦃...⦄</a> <a id="3278" class="Keyword">public</a>
</pre>
<hr />
<blockquote>
<p>知识共享许可协议: <a href="https://creativecommons.org/licenses/by-nc-sa/4.0/deed.zh">CC-BY-NC-SA 4.0</a><br />
<a href="https://github.com/choukh/MetaLogic/blob/main/src/FOL/Language.lagda.md">GitHub</a> | <a href="https://choukh.github.io/MetaLogic/FOL.Language.html">GitHub Pages</a> | <a href="https://www.yuque.com/ocau/metalogic/fol.language">语雀</a><br />
交流Q群: 893531731</p>
</blockquote>
</main>

<script>
;(function() {
  // Non-essential if user has JavaScript off. Just makes checkboxes look nicer.
  var selector = '.task-list > li > input[type="checkbox"]';
  var checkboxes = document.querySelectorAll(selector);
  Array.from(checkboxes).forEach((checkbox) => {
    var wasChecked = checkbox.checked;
    checkbox.disabled = false;
    checkbox.addEventListener('click', (ev) => {ev.target.checked = wasChecked});
  });
})();
</script>
</body>
</html>
